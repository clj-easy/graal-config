{:paths ["."]
 :tasks
 {:requires ([babashka.fs :as fs]
             [babashka.tasks :as tasks]
             [build-shared :as bs]
             [clojure.string :as str])
  :init
  (do
    (def windows? (str/starts-with? (System/getProperty "os.name")
                                    "Windows"))

    (defn clojure
      "Clojure defaulting to stderr output"
      [opts & args]
      (let [[opts args] (if (map? opts)
                          [opts args]
                          [nil (cons opts args)])
            [arg & args] args]
        (apply tasks/clojure opts (str "-J-Dclojure.main.report=stderr " arg) args))))

  uber {:doc "Build uberjar for testing"
        :task (let [uber-file (str "output-" bs/graalvm-version ".jar")]
                (clojure {:dir bs/dir} "-T:build uber" {:uber-file uber-file})
                uber-file)}

  graalvm {:doc "Checks GRAALVM_HOME env var"
           :task
           (let [env (System/getenv "GRAALVM_HOME")]
             (assert env "Set GRAALVM_HOME")
             env)}

  native-image
  {:doc     "Builds native image"
   :depends [graalvm uber]
   :task (do (apply shell {:dir bs/dir}
                    (str (fs/file graalvm
                                  "bin"
                                  (if windows?
                                    "native-image.cmd"
                                    "native-image")))
                    "-jar" uber
                    (concat
                     ["--initialize-at-build-time=example"
                      "--verbose"
                      "--no-fallback"
                      "--no-server"
                      "-J-Dclojure.spec.skip.macros=true"
                      "-J-Dclojure.compiler.direct-linking=true"
                      "-J-Xmx8g"
                      "-Ob"
                      "-H:+ReportExceptionStackTraces"
                      "-H:Name=native-test"]
                     (or (:flags (read-string (slurp (str bs/dir "/deps.edn")))) []))))}

  native-image-test
  {:doc "Run integration test"
   :depends [native-image]
   :task    (shell {:dir bs/dir}
                   (if windows?
                     "./native-test.exe"
                     "./native-test"))}

  recipe
  {:task
   (do
     (let [lib-dir (fs/file "config" bs/org bs/lib)
           conf-dir (fs/file lib-dir "resources" "META-INF" "native-image" bs/org bs/lib)]
       (fs/create-dirs conf-dir)
       (fs/copy-tree "recipe" lib-dir {:replace-existing true})
       (spit (fs/file conf-dir "native-image.properties") "")))}

  ,}
 ,}
